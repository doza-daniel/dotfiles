pkgname=vglst
provides=('st')
pkgver=0.8.1
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X with jellybeans-like colors and transparency.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"

source=(http://dl.suckless.org/st/st-$pkgver.tar.gz
        st-opacity.patch
        st-jellybeans-black-bg.patch
        st-font.patch)

sha256sums=('c4fb0fe2b8d2d3bd5e72763e80a8ae05b7d44dbac8f8e3bb18ef0161c7266926'
            'e300af379f5cdca20851b24969fabfdd29a59e580147082acad71ec06f430b39'
            'c16e6469fa2c6eb8c69b670f71c2dc551965ec5f4638f679728fb6ca9d299ef3'
            'a106ff8f21d30cbf7e070653684d1506b4d188f326d5dfe3fb70b17f4fdc3cef')

prepare() {
  cd $srcdir/st-$pkgver

  # skip terminfo which conflicts with nsurses
  sed -i '/tic /d' Makefile

  # do the patching
  patch < $srcdir/st-font.patch
  patch < $srcdir/st-opacity.patch
  patch < $srcdir/st-jellybeans-black-bg.patch
}

build() {
  cd $srcdir/st-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/st-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/st/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/st/README"
}
