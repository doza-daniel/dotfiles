pkgname='vglvim'
provides=('vim')
pkgver=8309.d60e0a1d2
pkgrel=1
pkgdesc="VIM: Vi IMproved"
arch=('i686' 'x86_64')
url="http://github.com/vim/vim"
license=('GPL')
depends=('gpm' 'ruby' 'lua' 'python2' 'python' 'acl')
optdepends=()
conflicts=('vim')
backup=()
options=()
source=("git://github.com/vim/vim.git")
md5sums=('SKIP')

pkgver() {
    cd "vim"
    echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "$srcdir/vim"

    (cd src && autoconf)

    py2cfg=`dirname $(find /usr/lib/python* -type f -name 'config\.c') | head -n1`

    ./configure \
        --prefix=/usr \
        --with-compiledby='Arch Linux' \
	--with-features=huge \
        --enable-multibyte \
        --enable-rubyinterp \
        --enable-luainterp \
        --enable-perlinterp \
        --enable-pythoninterp \
	--with-python-config-dir=$py2cfg \
        --disable-python3interp \
        --enable-cscope \
        --with-x=yes \
        --disable-gui
	
    make VIMRUNTIMEDIR=/usr/share/vim/vim80
}

package() {
  cd "$srcdir/vim"

  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/bin/{ex,view}

  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1
    rm -f evim.1
  done

  install -Dm644 runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
